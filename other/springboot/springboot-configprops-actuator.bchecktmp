metadata:
    language: v1-beta
    name: "Springboot configprops actuator"
    description: "Springboot configprops actuator may expose sensitive information"
    author: "Mateusz Dabrowski (dbrwsky)"
	tags: "actuator", "springboot", "exposure", "informative"

run for each:
    potential_path =
        "/configprops",
        "/actuator/configprops"

given host then
    send request called check:
        method: "GET"
        path: {potential_path}

    if {check.response.status_code} is "200" 
		and "org.springframework.boot.actuate" in {check.response.body} 
		and "\"beans\"" in {check.response.body}
		and  "\"contexts\"" in {check.response.body}
		and ("application/json" in {check.response.headers} 
			or "application/vnd.spring-boot.actuator" in {check.response.headers} 
			or "application/vnd.spring-boot.actuator.v1+json" in {check.response.headers} 
			or "application/vnd.spring-boot.actuator.v2+json" in {check.response.headers}
			or "application/vnd.spring-boot.actuator.v3+json" in {check.response.headers}) then
				report issue:
					severity: low
					confidence: certain
					detail: `Springboot configprops actuator found at {potential_path}.`
					remediation: "Ensure configprops is not exposed or doesn't contain sensitive information."
	end if